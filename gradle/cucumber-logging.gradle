import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import groovy.time.TimeCategory
import java.text.MessageFormat

def testResults = []
def testFailures = 0

tasks.named("test") { testTask ->

    doLast {
        println 'TEST FINISHED'
    }

    testLogging {
        events "failed"
        exceptionFormat TestExceptionFormat.FULL
        showExceptions true
        showCauses true
        showStackTraces true
    }

    ignoreFailures = true // Always try to run all tests for all modules
    afterSuite { descriptor, result ->

        if (descriptor.parent) return // Only summarize results for whole modules
        if (descriptor.getParent() == null) {
            testFailures += result.getFailedTestCount() //count failures
        }

        String summary = "${testTask.project.name}:${testTask.name} results: ${result.resultType} " +
                "(" +
                "${result.testCount} tests, " +
                "${result.successfulTestCount} successes, " +
                "${result.failedTestCount} failures, " +
                "${result.skippedTestCount} skipped" +
                ") " +
                "in ${TimeCategory.minus(new Date(result.endTime), new Date(result.startTime))}" +
                "\n" +
                "Report file: ${testTask.reports.html.entryPoint}" + "\n" +
                "Cucumber report: ${project.projectDir}/build/reports/tests/test/cucumber-report.html" + "\n" +
                "Jacoco report: ${project.projectDir}/build/reports/jacoco/test/html/index.html"


        // Add reports in `testResults`, keep failed suites at the end
        if (result.resultType == TestResult.ResultType.SUCCESS) {
            testResults.add(0, summary)
        } else {
            testResults += summary
        }
        printResults new Object[]{summary}
    }
}

gradle.buildFinished {
  if (!testResults.isEmpty()) {
    printResults testResults
  }
  if (testFailures > 0) { // check if any tests failed
      String message = new MessageFormat("There {0,choice,0#are no|1#is one|were {0,number}} failing {0,choice,1#test|tests}.")
              .format(new Object[]{testFailures});

      throw new VerificationException(message)
  }
}

private static void printResults(allResults) {
    def maxLength = allResults*.readLines().flatten().collect { it.length() }.max()

    println "┌${"${"─" * maxLength}"}┐"

    println allResults.collect {
        it.readLines().collect {
            "│" + it + " " * (maxLength - it.length()) + "│"
        }.join("\n")
    }.join("\n├${"${"─" * maxLength}"}┤\n")

    println "└${"${"─" * maxLength}"}┘"
}