
buildscript {
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
	}
}

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.0'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'com.github.johnrengelman.shadow' version '7.1.2'
	id 'jacoco'
	id "org.sonarqube" version "4.2.1.3168"
//	id 'checkstyle'
}


group = 'gov.cdc.dataingestion'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

tasks.withType(Jar).all { duplicatesStrategy = DuplicatesStrategy.EXCLUDE }

configurations {
	jaxb
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

jacoco {
	toolVersion = "0.8.8"
	reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

test {
	finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
	getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
}
task integration(type: Test) {
	useJUnitPlatform()
}

jar {
	manifest {
		attributes(
				'Main-Class': 'gov.cdc.dataingestion.ServiceApplication'
		)
	}
	zip64 true
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

task jaxb {
	System.setProperty('javax.xml.accessExternalSchema', 'all')
	def jaxbTargetDir = file("src/main/java")
	doLast {
		jaxbTargetDir.mkdirs()
		ant.taskdef(
				name: 'xjc',
				classname: 'com.sun.tools.xjc.XJCTask',
				classpath: configurations.jaxb.asPath
		)
		ant.jaxbTargetDir = jaxbTargetDir
		ant.xjc(
				destdir: '${jaxbTargetDir}',
				package: 'gov.cdc.dataingestion.nbs.jaxb',
				schema: 'src/main/resources/xsd/PHDC.xsd'
		)
	}
}

compileJava.dependsOn jaxb

dependencies {
	implementation project(':hl7-parser')
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'commons-lang:commons-lang:2.6'

	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.kafka:spring-kafka:3.0.6'
	implementation 'org.apache.kafka:kafka-clients:2.8.1'
	implementation 'ca.uhn.hapi.fhir:hapi-fhir-structures-r4:6.1.3'
	implementation 'io.github.linuxforhealth:hl7v2-fhir-converter:1.0.19'
	implementation 'com.google.code.gson:gson:2.10.1'
	implementation 'com.opencsv:opencsv:5.7.1'
	implementation 'com.microsoft.sqlserver:mssql-jdbc:12.2.0.jre11'
	implementation 'com.zaxxer:HikariCP:5.0.1'

	implementation 'ca.uhn.hapi:hapi-base:2.3'
	implementation 'ca.uhn.hapi:hapi-structures-v251:2.3'
	implementation 'ca.uhn.hapi:hapi-structures-v231:2.3'
	implementation 'org.springframework.boot:spring-boot-starter-logging'
	implementation 'org.slf4j:slf4j-api'
	implementation 'ch.qos.logback:logback-classic'
	implementation 'net.logstash.logback:logstash-logback-encoder:6.6'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.4'
	implementation 'software.amazon.msk:aws-msk-iam-auth:1.0.0'

	// DI HL7 Wrapper
	if (findProject(':hl7-parser')) {
		implementation project(':hl7-parser')
	} else {
		// this mostly needed when testing the app on idea
		implementation files('libs/hl7-parser-0.0.1-SNAPSHOT.jar')
	}

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation(platform('org.junit:junit-bom:5.9.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testImplementation 'org.springframework.kafka:spring-kafka-test:3.0.6'

	testImplementation(platform('org.junit:junit-bom:5.9.1'))
	testImplementation('org.junit.jupiter:junit-jupiter:5.9.2')

	testImplementation 'org.testcontainers:testcontainers:1.17.3'
	testImplementation "org.testcontainers:kafka:1.17.3"
	testImplementation "org.testcontainers:mssqlserver:1.17.3"
	testImplementation "org.testcontainers:junit-jupiter:1.17.3"
	testImplementation 'org.testcontainers:postgresql:1.17.3'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	runtimeOnly 'org.postgresql:postgresql:42.5.4'

	runtimeOnly 'org.springframework.boot:spring-boot-devtools'

	// Below is needed to compile jaxb generated code
	implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'

	// Below is needed for runtime with jaxb generated code
	runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.2'

	// Only need below two dependencies for java code generation from xsd file(s)
	jaxb 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
	jaxb 'org.glassfish.jaxb:jaxb-xjc:4.0.2'
}

test {
	useJUnitPlatform()
}

jar {
	// task configuration goes here
	if (findProject(':hl7-parser')) {
		dependsOn(':hl7-parser:jar')
		from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	}
}

sonarqube {
	properties {
		property "sonar.projectKey", "CDCgov_NEDSS-DataIngestion"
		property "sonar.organization", "cdcgov"
		property "sonar.host.url", "https://sonarcloud.io"
	}
}