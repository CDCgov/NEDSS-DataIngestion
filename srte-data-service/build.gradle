buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
        classpath 'org.owasp:dependency-check-gradle:9.0.3'
    }
}


plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'jacoco'
    id "org.sonarqube" version "4.2.1.3168"
}

apply plugin: 'org.owasp.dependencycheck'
group = 'gov.cdc.rti'
version = '0.0.1-SNAPSHOT'

springBoot {
    buildInfo()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

bootRun {
  systemProperty('spring.profiles.active', 'default,local')

    debugOptions {
        enabled = true
        server = true
        suspend = false
        port = java.util.Optional.ofNullable(findProperty('debug.port'))
                .map(Object::toString)
                .map(Integer::parseInt)
                .orElse(19043)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Swagger - openapi
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // Database
    implementation 'com.microsoft.sqlserver:mssql-jdbc:12.2.0.jre11'
    implementation 'org.hibernate.validator:hibernate-validator'

    // Lombok annotations
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Misc
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'com.zaxxer:HikariCP:5.0.1'
    implementation 'org.apache.httpcomponents.core5:httpcore5:5.2.5'
    runtimeOnly 'com.nimbusds:oauth2-oidc-sdk:11.8'

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.testcontainers:mssqlserver'
}

tasks.named('test') {
    useJUnitPlatform()
}

test {
    if (System.getProperty("testing.database.mssql.image") != null) {
        systemProperty "testing.database.mssql.image", System.getProperty("testing.database.mssql.image")
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "CDCgov_NEDSS-DataIngestion"
        property "sonar.organization", "cdcgov"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions",  "**/srtedataservice/**"
    }
}